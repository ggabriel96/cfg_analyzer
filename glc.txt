<lpar> ::= "("
<rpar> ::= ")"
<lbrack> ::= "{"
<rbrack> ::= "}"
<comma> ::= ","
<semicolon> ::= ";"
<not> ::= "!"
<lines> ::= <line><lines> | ""
<line> ::= <if> | <for> | <while> | <declaration> | <variable_assignment>
<if> ::= "if" <lpar> <expression> <rpar> <if'>
<if'> ::= <line> | <lbrack> <lines> <rbrack> <else>
<else> ::= "else" <else'> | ""
<else'> ::= <line> | <lbrack> <lines> <rbrack>
<for> ::= "for" <lpar> <for'> <semicolon> <for_condition> <semicolon> <for'> <rpar> <for''>
<for'> ::= <assignment_list> | ""
<for_condition> ::= <expression> | ""
<for''> ::= <line> | <lbrack> <lines> <rbrack>
<while> ::= "while" <lpar> <expression> <rpar> <while'>
<while'> ::= <line> | <lbrack> <lines> <rbrack>
<declaration> ::= "let" <declaration_list> <semicolon>
<declaration_list> ::= <variable><declaration_list'>
<declaration_list'> ::= <assignment><declaration_list''> | <declaration_list''>
<declaration_list''> ::= <comma> <declaration_list> | ""
<variable_assignment> ::= <assignment_list> <semicolon>
<assignment_list> ::= <variable><assignment><assignment_list'>
<assignment_list'> ::= <comma> <assignment_list> | ""
<assignment> ::= "=" <expression>
<expression> ::= <lpar> <expression> <rpar> <expression'> | <not> <expression><expression'> | <operand><expression'>
<expression'> ::= <operator><expression><expression'> | ""
*<variable> ::= <letter><variable'> | "_" <variable'>
+<variable'> ::= <letter><variable'> | "_" <variable'> | <digit><variable'> | ""
*<operand> ::= <variable> | <literal>
<operator> ::= "+" | "-" | "*" | "/" | "%" | "^" | "or" | "and" | "<=" | "<" | "==" | "!=" | ">" | ">="
+<letter> ::= "a" | "b" | "c" | "d" | "e" | "f" | "g" | "h" | "i" | "j" | "k" | "l" | "m" | "n" | "o" | "p" | "q" | "r" | "s" | "t" | "u" | "v" | "w" | "x" | "y" | "z" | "A" | "B" | "C" | "D" | "E" | "F" | "G" | "H" | "I" | "J" | "K" | "L" | "M" | "N" | "O" | "P" | "Q" | "R" | "S" | "T" | "U" | "V" | "W" | "X" | "Y" | "Z"
+<digit> ::= "0" | "1" | "2" | "3" | "4" | "5" | "6" | "7" | "8" | "9"
*<literal> ::= "'" <ascii> "'" | <number> | <string>
+<ascii> ::= <ascii_str> | "\\" | "\""
+<ascii_str> ::= <letter> | <digit> | <special_char>
*<number> ::= "+" <number'> | "-" <number'> | <number'>
+<number'> ::= <digit><number''><number'''>
+<number''> ::= <digit><number''> | ""
+<number'''> ::= "." <digit><number''> | ""
*<string> ::= "\"" <string_char>
+<string_char> ::= "\\"<ascii><string_char> | <ascii_str><string_char> | "\""
+<special_char> ::= "!" | "@" | "#" | "$" | "%" | "&" | "*" | "(" | ")" | "-" | "_" | "=" | "+" | "'" | "[" | "]" | "{" | "}" | "^" | "~" | "`" | "/" | "?" | ";" | ":" | "," | "." | "<" | ">" | "|"
